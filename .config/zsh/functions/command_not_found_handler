emulate -LR zsh
local command="${1-}"
shift 1

local LF=$'\n'

internal::ask4search() {
    internal::fmt::prompt 'Search for packages providing the command?' 'Searching Packages'
}

internal::fmt::prompt() {
    local question="$1"
    local searchingstr="$2"
    internal::fmt::println --no-newline --fg=255 --bg=31 --nxtbg=46 $question
    internal::fmt::println --no-newline --fg=232 --bg=46 --nxtbg=124 Y
    internal::fmt::println --no-newline --fg=255 --bg=124 N
    print -n ' '
    read -rq || return 1 # if the line is 'y' or 'Y', it returns 0. Otherwise, it returns 1.
    print -n '[2K\r'
    internal::fmt::println --no-newline --fg=255 --bg=31 $searchingstr
    print -n '\r'
}

internal::fmt::println() {
    local i bg fg nxtbg bold str="%kî‚¼%f" line
    local -a txt=()
    for i in "$@"; do
        case "${i:=}" in
            --bg=*) bg="${i#*=}" ;;
            --fg=*) fg="${i#*=}" ;;
            --nxtbg=*) nxtbg="${i#*=}" ;;
            --no-newline) unset line ;;
            --no-bold) unset bold ;;
            *) txt+=("$i") ;;
        esac
    done
    [[ -n ${nxtbg:-} ]] && str="%K{$nxtbg}î‚¼%f%k"
    print -Pn "${bold+%B}%F{$fg}%K{$bg} ${(j: :)txt} ${bold+%b}%F{$bg}${str}${line+$LF}"
}

internal::fmt::println --no-newline --fg=232 --bg=196 --nxtbg=52 ó°…— ERROR
internal::fmt::println --fg=255 --no-bold --bg=52 "command '%B${command}%b' not found!"

if [[ ${ZSHRC_LOADED:-} == false ]]; then
    print "$command '${(j:' ':)@}'" >>"$HOME/.zsh-faildeps" # (j: :) bypasses IFS, more reliable than $*
elif [[ ! -t 0 || ! -t 1 || ! -t 2 ]]; then
    : # filter out non-interactive terminals
elif command -v dnf &>/dev/null && internal::ask4search; then
    # internal::fmt::println --fg=255 --bg=52 "dnf is not implemented yet! look at https://command-not-found.com"
    local -a packages=("${(@f)$(dnf repoquery --qf "%{reponame}\t%{name}\t%{version}" --latest-limit 1 --whatprovides /usr/bin/$command 2>/dev/null || :)}")
    ((${#packages[1]})) || packages=("${(@f)$(dnf repoquery --qf "%{reponame}\t%{name}\t%{version}" --latest-limit 1 --whatprovides $command 2>/dev/null || :)}")
    print -n '[2K\r'
    if [[ -n ${packages[1]-} ]]; then
        internal::fmt::println --fg=255 --bg=31 'Available packages'
        local i bgc fgc repo pkg vrsn
        for i in $packages[@]; do
            # [[ -z ${i:-} || ${i:-} != *'/'* ]] && print $i && continue
            repo="${i%%$'\t'*}"
            pkg="${i#*$'\t'}"
            pkg="${pkg%$'\t'*}"
            vrsn="${i##*$'\t'}"
            case $repo in
                fedora) bgc=17 fgc=75 ;;
                updates) bgc=82 fgc=22 ;;
                terra) bgc=117 fgc=21 ;;
                charm) bgc=171 fgc=255 ;;
                'fedora-cisco-openh264') bgc=51 fgc=23 ;;
                'rpmfusion-free') bgc=40 fgc=22 ;;
                'rpmfusion-free-updates') bgc=46 fgc=22 ;;
                'rpmfusion-free-tainted') bgc=52 fgc=196 ;;
                'rpmfusion-nonfree') bgc=94 fgc=175 ;;
                'rpmfusion-nonfree-updates') bgc=166 fgc=185 ;;
                'rpmfusion-nonfree-tainted') bgc=52 fgc=196 ;;
                *) bgc=250 fgc=235 ;;
            esac
            internal::fmt::println --no-newline --fg=$fgc --bg=$bgc --nxtbg=$fgc $repo
            internal::fmt::println --no-newline --fg=$bgc --bg=$fgc --nxtbg=$bgc $pkg
            internal::fmt::println --fg=$fgc --bg=$bgc $vrsn
        done
    else
        internal::fmt::println --no-bold --fg=255 --bg=52 "No packages matching '%B${command}%b' found!"
    fi
elif command -v pacman pkgfile &>/dev/null && internal::ask4search; then
    local -a packages=("${(@f)$(pkgfile -b $command || :)}")
    print -n '[2K\r'
    if [[ -n ${packages[1]-} ]]; then
        internal::fmt::println --fg=255 --bg=31 'Available packages'
        local i bgc fgc repo pkg
        for i in $packages[@]; do
            [[ -z ${i:-} || ${i:-} != *'/'* ]] && print $i && continue
            repo="${i%%/*}"
            pkg="${i#*/}"
            case $repo in
                core) bgc=226 fgc=17 ;;
                extra) bgc=82 fgc=22 ;;
                multilib) bgc=51 fgc=23 ;;
                g14) bgc=165 fgc=54 ;;
                chaotic-aur) bgc=198 fgc=52 ;;
                aur) bgc=33 fgc=17 ;;
                *) bgc=250 fgc=235 ;;
            esac
            internal::fmt::println --no-newline --fg=$fgc --bg=$bgc --nxtbg=$fgc $repo
            internal::fmt::println --fg=$bgc --bg=$fgc $pkg
        done
    else
        internal::fmt::println --no-bold --fg=255 --bg=52 "No packages matching '%B${command}%b' found!"
    fi
# else
    # internal::fmt::println --fg=255 --bg=55 "Your package manager is not implemented yet! Try looking at https://command-not-found.com"
fi

print -n '[0m'
return 127
# vim:ft=sh
