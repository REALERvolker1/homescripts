# vim:ft=zsh
emulate -L zsh
setopt EXTENDED_GLOB

local -a plugins
local i

# echo 'command -v atuin &>/dev/null || return'
# echo '[[ "$TERM" == "linux" ]] && return'
if command -v atuin &>/dev/null; then
    (
        echo '[[ "$TERM" != "linux" ]] && command -v atuin &>/dev/null || return'
        atuin init zsh | grep -vE '(echoti|^[[:space:]]*#|^[[:space:]]*$)'
    ) >"$ZDOTDIR/rc.d/60-atuin.defer.zsh"
fi

plugins+=(
    "$ZDOTDIR/rc.d/"*.zsh(.N)
    "$ZDOTDIR/.zshrc"
    "$ZDOTDIR/.zshenv"
    "$ZDOTDIR/.zprofile"
    # plugins
    "$ZPLUGIN_DIR/fzf-tab/"fzf-tab.zsh
    "$ZPLUGIN_DIR/fzf-tab/lib/"^*.zwc(.N)
    "$ZPLUGIN_DIR/zsh-autosuggestions/zsh-autosuggestions.zsh"
    # I kept getting file write errors
    #"$ZPLUGIN_DIR/fast-syntax-highlighting/â†’chroma/"^*.zwc(.N)
)

foreach i ($plugins) {
    [[ -f "$i.zwc" ]] && command -p rm -f "$i.zwc" && print -P "%F{9}Removed %B${i}.zwc%b%f"
    [[ -f $i ]] && zcompile -Uz "$i" && print -P "%F{10}Compiled %B${i}%b%f"
}
